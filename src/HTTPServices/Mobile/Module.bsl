#Область ОбработчикиСобытий
Функция ПроверитьДоступность(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки("Ок");
	Возврат Ответ;
КонецФункции

Функция ПолучитьДанные(Запрос)
	
	Попытка
	
		ИдентификаторУстройства = Запрос.Заголовки.Получить("X-MobileID");
		
		Узел = ПланыОбмена.ОбменСМобильным.НайтиПоКоду(ИдентификаторУстройства);
		
		Если НЕ ЗначениеЗаполнено(Узел) Тогда
			УзелОбъект = ПланыОбмена.ОбменСМобильным.СоздатьУзел();
			УзелОбъект.Наименование = ИдентификаторУстройства;
			УзелОбъект.Код = ИдентификаторУстройства;
			УзелОбъект.Пользователь = Пользователи.ТекущийПользователь();
			УзелОбъект.Записать();
			Узел = УзелОбъект.Ссылка;
			НачальнаяРегистрация(Узел);
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ЗаказПокупателяИзменения.Ссылка КАК Ссылка,
		               |	ЗаказПокупателяИзменения.Ссылка.Номер КАК Номер,
		               |	ЗаказПокупателяИзменения.Ссылка.Дата КАК Дата,
		               |	ЗаказПокупателяИзменения.Ссылка.Комментарий КАК Комментарий,
		               |	ЗаказПокупателяИзменения.Ссылка.СуммаДокумента КАК СуммаДокумента
		               |ИЗ
		               |	Документ.ЗаказПокупателя.Изменения КАК ЗаказПокупателяИзменения
		               |ГДЕ
		               |	ЗаказПокупателяИзменения.Узел = &Узел";
		Запрос.УстановитьПараметр("Узел", Узел);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		ДанныеДляОтправки = Новый Массив;
		ОтправленныеОбъекты = Новый Массив;
		
		Пока Выборка.Следующий() Цикл
			
			ДанныеДокумента = Новый Структура;
			ДанныеДокумента.Вставить("Идентификатор", Строка(Выборка.Ссылка.УникальныйИдентификатор()));
			ДанныеДокумента.Вставить("Номер", Выборка.Номер);
			ДанныеДокумента.Вставить("Дата", ЗаписатьДатуJSON(Выборка.Дата, ФорматДатыJSON.ISO));
			ДанныеДокумента.Вставить("Комментарий", Выборка.Комментарий);
			ДанныеДокумента.Вставить("СуммаДокумента", Выборка.СуммаДокумента);
			
			ДанныеДляОтправки.Добавить(ДанныеДокумента);
			
			ОтправленныеОбъекты.Добавить(Выборка.Ссылка);
			
		КонецЦикла;
		
		ТелоОтвета = ЗаписатьЗначениеJSON(ДанныеДляОтправки);
		
		Если ЗначениеЗаполнено(ОтправленныеОбъекты) Тогда
			ПланыОбмена.УдалитьРегистрациюИзменений(Узел, ОтправленныеОбъекты);
		КонецЕсли;
		
	Исключение
		
		ЗаписьЖурналаРегистрации("ОбменСМобильным.Ошибка", УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
		
	КонецПопытки;
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-Type", "application/json");
	Ответ.УстановитьТелоИзСтроки(ТелоОтвета);
	Возврат Ответ;
	
КонецФункции


#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура НачальнаяРегистрация(Узел)
	//Получаем данные по всем документам пользователя
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказПокупателя.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаказПокупателя КАК ЗаказПокупателя
		|ГДЕ
		|	ЗаказПокупателя.Ответственный = &Ответственный";
	Запрос.УстановитьПараметр("Ответственный", Пользователи.ТекущийПользователь());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДанныеДляРегистрации = Новый Массив;
	//Добавляем результат выборки в массив
	Пока Выборка.Следующий() Цикл
		ДанныеДляРегистрации.Добавить(Выборка.Ссылка);
	КонецЦикла;
	//Если массив не пустой регистрируем все к обмену
	Если ЗначениеЗаполнено(ДанныеДляРегистрации) Тогда
		ПланыОбмена.ЗарегистрироватьИзменения(Узел, ДанныеДляРегистрации);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьДанныеПоЗаказам(Пользователь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КОЛИЧЕСТВО(ЗаказПокупателя.Ссылка) КАК Количество
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ответственный = &Ответственный";
	
	Запрос.УстановитьПараметр("Ответственный", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	РезультатЗапроса.Следующий();
	
	Возврат РезультатЗапроса.Количество;
	
КонецФункции
#КонецОбласти