&НаСервере
Функция ОбработатьВходящийЗапрос(Запрос) Экспорт
	
	ИдентификаторУстройства = Запрос.Заголовки.Получить("X-MobileID");
	
	ТелоОтвета = ПодготовитьТелоОтвета(ИдентификаторУстройства);
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.Заголовки.Вставить("Content-Type", "application/json");
	Ответ.УстановитьТелоИзСтроки(ТелоОтвета);
	Возврат Ответ;
	
КонецФункции

&НаСервере
Функция ПодготовитьТелоОтвета(ИдентификаторУстройства)Экспорт
	
	Узел = ПланыОбмена.ОбменСМобильным.НайтиПоКоду(ИдентификаторУстройства);
	Если НЕ ЗначениеЗаполнено(Узел) Тогда
		УзелОбъект = ПланыОбмена.ОбменСМобильным.СоздатьУзел();
		
		Пользователь = Пользователи.ТекущийПользователь();
		
		УзелОбъект.Наименование = СтрШаблон("Мобильное устройство (%1)", Пользователь);
		УзелОбъект.Код = ИдентификаторУстройства;
		УзелОбъект.Пользователь = Пользователь;
		УзелОбъект.Записать();
		Узел = УзелОбъект.Ссылка;
		НачальнаяРегистрация(Узел);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоставкаИзменения.Ссылка КАК Ссылка,
	|	ДоставкаИзменения.Ссылка.Номер КАК Номер,
	|	ДоставкаИзменения.Ссылка.Дата КАК Дата,
	|	ДоставкаИзменения.Ссылка.Контрагент КАК Контрагент,
	|	ДоставкаИзменения.Ссылка.АдресДоставки КАК АдресДоставки,
	|	ДоставкаИзменения.Ссылка.Комментарий КАК Комментарий,
	|	ДоставкаИзменения.Ссылка.Товары.(
	|		Номенклатура КАК Номенклатура,
	|		Количество КАК Количество
	|	) КАК Товары
	|ИЗ
	|	Документ.КЛ_Доставка.Изменения КАК ДоставкаИзменения
	|ГДЕ
	|	ДоставкаИзменения.Узел = &Узел
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтрагентыИзменения.Ссылка КАК Ссылка,
	|	КонтрагентыИзменения.Ссылка.Код КАК Код,
	|	КонтрагентыИзменения.Ссылка.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Контрагенты.Изменения КАК КонтрагентыИзменения
	|ГДЕ
	|	КонтрагентыИзменения.Узел = &Узел
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураИзменения.Ссылка КАК Ссылка,
	|	НоменклатураИзменения.Ссылка.Код КАК Код,
	|	НоменклатураИзменения.Ссылка.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Номенклатура.Изменения КАК НоменклатураИзменения
	|ГДЕ
	|	НоменклатураИзменения.Узел = &Узел";
	
	Запрос.УстановитьПараметр("Узел", Узел);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеДляОтправки = Новый Массив;
	ОтправленныеОбъекты = Новый Массив;
	
//Добавляем контрагентов для отправки
	Если НЕ РезультатЗапроса[1].Пустой() Тогда
		ВыборкаКонтрагент = РезультатЗапроса[1].Выбрать();
		
		Пока ВыборкаКонтрагент.Следующий() Цикл
			
			ДанныеКонтрагента = Новый Структура;
			ДанныеКонтрагента.Вставить("ВидОбъекта", "Контрагент");
			ДанныеКонтрагента.Вставить("Идентификатор", Строка(ВыборкаКонтрагент.Ссылка.УникальныйИдентификатор()));
			ДанныеКонтрагента.Вставить("Код", ВыборкаКонтрагент.Код);
			ДанныеКонтрагента.Вставить("Наименование", ВыборкаКонтрагент.Наименование);
			//ДанныеКонтрагента.Вставить("Комментарий", Выборка.Комментарий);
			
			ДанныеДляОтправки.Добавить(ДанныеКонтрагента);
			
			ОтправленныеОбъекты.Добавить(ВыборкаКонтрагент.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
//Добавляем номенклатуру для отправки
	Если НЕ РезультатЗапроса[2].Пустой() Тогда
		ВыборкаНоменклатура = РезультатЗапроса[2].Выбрать();
		
		Пока ВыборкаНоменклатура.Следующий() Цикл
			
			ДанныеНоменклатура = Новый Структура;
			ДанныеНоменклатура.Вставить("ВидОбъекта", "Номенклатура");
			ДанныеНоменклатура.Вставить("Идентификатор", Строка(ВыборкаНоменклатура.Ссылка.УникальныйИдентификатор()));
			ДанныеНоменклатура.Вставить("Код", ВыборкаНоменклатура.Код);
			ДанныеНоменклатура.Вставить("Наименование", ВыборкаНоменклатура.Наименование);
			//ДанныеНоменклатура.Вставить("Комментарий", Выборка.Комментарий);
			
			ДанныеДляОтправки.Добавить(ДанныеНоменклатура);
			
			ОтправленныеОбъекты.Добавить(ВыборкаНоменклатура.Ссылка);
			
		КонецЦикла;
	КонецЕсли;
	
//Добавляем документы для отправки	
Если НЕ РезультатЗапроса[0].Пустой() Тогда
		ВыборкаДокумент = РезультатЗапроса[0].Выбрать();
		
		Пока ВыборкаДокумент.Следующий() Цикл
			
			ДанныеДокумента = Новый Структура;
			ДанныеДокумента.Вставить("ВидОбъекта", "Документ");
			ДанныеДокумента.Вставить("Идентификатор", Строка(ВыборкаДокумент.Ссылка.УникальныйИдентификатор()));
			ДанныеДокумента.Вставить("Номер", ВыборкаДокумент.Номер);
			ДанныеДокумента.Вставить("Дата", ЗаписатьДатуJSON(ВыборкаДокумент.Дата, ФорматДатыJSON.ISO));
			ДанныеДокумента.Вставить("Комментарий", ВыборкаДокумент.Комментарий);
			
			ДанныеДляОтправки.Добавить(ДанныеДокумента);
			
			ОтправленныеОбъекты.Добавить(ВыборкаДокумент.Ссылка);
		КонецЦикла;
	КонецЕсли;
		
	ТелоОтвета = ЗаписатьЗначениеJSON(ДанныеДляОтправки);
	
	Если ЗначениеЗаполнено(ОтправленныеОбъекты) Тогда
		ПланыОбмена.УдалитьРегистрациюИзменений(Узел, ОтправленныеОбъекты);
	КонецЕсли;
	
	Возврат ТелоОтвета
	
КонецФункции

&НаСервере
Процедура НачальнаяРегистрация(Узел)
	//Получаем данные по всем документам пользователя
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Доставка.Ссылка КАК Доставка
	|ИЗ
	|	Документ.КЛ_Доставка КАК Доставка
	|ГДЕ
	|	Доставка.Ответственный = &Ответственный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.ВидНоменклатуры = &ВидНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Контрагент
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты";
	
	Запрос.УстановитьПараметр("Ответственный", Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("ВидНоменклатуры", Перечисления.ВидыНоменклатуры.Товар);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	//Добавляем результат выборки в массив	
	ДанныеДляРегистрации = Новый Массив;
	
	Если НЕ РезультатЗапроса[0].Пустой() Тогда
		ВыборкаДокумент = РезультатЗапроса[0].Выбрать();
		
		Пока ВыборкаДокумент.Следующий() Цикл
			
			ДанныеДляРегистрации.Добавить(ВыборкаДокумент.Доставка);
			
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ РезультатЗапроса[1].Пустой() Тогда
		ВыборкаНоменклатура = РезультатЗапроса[1].Выбрать();
		
		Пока ВыборкаНоменклатура.Следующий() Цикл
			
			ДанныеДляРегистрации.Добавить(ВыборкаНоменклатура.Номенклатура);
			
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ РезультатЗапроса[2].Пустой() Тогда
		ВыборкаКонтрагент = РезультатЗапроса[2].Выбрать();
		
		Пока ВыборкаКонтрагент.Следующий() Цикл
			
			ДанныеДляРегистрации.Добавить(ВыборкаКонтрагент.Контрагент);
			
		КонецЦикла;
	КонецЕсли;
	
	//Если массив не пустой регистрируем все к обмену
	Если ЗначениеЗаполнено(ДанныеДляРегистрации) Тогда
		ПланыОбмена.ЗарегистрироватьИзменения(Узел, ДанныеДляРегистрации);
	КонецЕсли;
	
КонецПроцедуры
